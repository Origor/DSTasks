type key.
type NONCE. (* creates a new datatype with the purpose of being a nonce. *)

fun senc(bitstring, key): bitstring.

reduc forall m: bitstring, k: key; sdec(senc(m, k), k) = m.

type skey.
type pkey.

fun pk(skey): pkey.
fun aenc(bitstring, pkey): bitstring.

reduc forall m: bitstring, k: skey; adec(aenc(m, pk(k)), k) = m.

type sskey.
type spkey.

fun spk(sskey): spkey.
fun sign(bitstring, sskey): bitstring.

reduc forall m: bitstring, k: sskey; getmess(sign(m, k)) = m.
reduc forall m: bitstring, k: sskey; checksign(sign(m, k), spk(k)) = m.

free c: channel.
free s: bitstring [ private ].

event aliceReceiveKey(key).
event bobFreshKey(key).

event aliceAccepts(key).
event bobAccepts(key, pkey).
event aliceTerm(key, pkey).
event bobTerm(key).

query attacker(s).

(* query skx: key, pky: pkey; event(aliceTerm(skx,pky)) ==> event(bobAccepts(skx,pky)).
 The above does not hold for injective version inj-event, meaning
   that the replay attack on Alice is possible. Fix the protocol so that the 
   above query verifies also with inj-event. *)

query x: key; inj-event(aliceReceiveKey(x)) ==> inj-event(bobFreshKey(x)).
query skx: key, pky: pkey; inj-event(aliceTerm(skx, pky)) ==> inj-event(bobAccepts(skx, pky)).

query skx: key; inj-event(bobTerm(skx)) ==> inj-event(aliceAccepts(skx)).

let alice(pkA: pkey, skA: skey, pkB: spkey) =
  new nonce: NONCE; (* creates the nonce. *)	   
  out(c, (pkA, nonce)); (* sends the nonce created above. *)
  in(c, x: bitstring);
  let sigB = adec(x, skA) in
  let (=pkA, =pkB, =nonce, k: key) = checksign(sigB, pkB) in
  event aliceReceiveKey(k);
  event aliceAccepts(k);
  out(c, senc(s, k));
  event aliceTerm(k, pkA).

let bob(pkB: spkey, skB: sskey, pkA: pkey) =
  in(c, (pkX: pkey, nonce2: NONCE)); (* adds a nonce to bob. *)
  new k: key;
  event bobFreshKey(k);
  event bobAccepts(k, pkX);
  out(c, aenc(sign((pkX, pkB, nonce2, k), skB), pkX)); (* bob signs with nonce2 *)
  in(c, x:bitstring);
  let z = sdec(x, k) in 
  if pkX = pkA then event bobTerm(k).

process
  new skA: skey; new skB: sskey;
  let pkA = pk(skA) in out(c, pkA);
  let pkB = spk(skB) in out(c, pkB);
  ( (!alice(pkA, skA, pkB)) | (!bob(pkB, skB, pkA)) )
  